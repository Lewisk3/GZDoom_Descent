class Descent_PhoenixCannon : DSCWeapon
{
	Default
	{
		Weapon.SlotNumber 7;
		Tag "Phoenx";
		DSCWeapon.WeaponName "Phoenix Cannon";
		DSCWeapon.DisplayGraphic "WeapPhoenix";
		DSCWeapon.SetupCrosshairs "Crosshair/weapon1", "Crosshair/weapon0";
		DSCWeapon.EnergyGive 8;
	}
		
	action void A_FirePhoenixCannon()
	{
		A_ShootProjectile("DSCProj_Phoenix", "PlasmaCannon/Fire", true, 0,0,0, (10,-10, -5));
		A_ShootProjectile("DSCProj_Phoenix", useammo:true, 0,0,0, (10, 10, -5));
	}
	
	States
	{
		Ready:
			TNT1 A 1 A_WeaponReady();
		Loop;
		
		Fire:
			TNT1 A 6 A_FirePhoenixCannon();
		Goto Ready;
		
		Spawn:
			PNIX ABCDEFGHIJKLMNO 5;
		loop;
	}
}

// キッケリキー！www
class DSCProj_Phoenix : DSCProj_Plasma
{
	Default
	{	
		Radius 5;
		Height 5;
		Speed 20;
		DamageFunction 14 * DSCDMG;
		Scale 0.35;
		DSCProjectile.EnergyUse 0.35;
		Decal "Scorch";
		
		BounceType "Hexen";
		BounceFactor 1.0;
		WallBounceFactor 1.0;
		BounceCount 3;
	}
	
	States
	{
		Spawn:
			PHNX ABC 3 Bright
			{
				bHITOWNER = true;
			}
		loop;
		
		Crash:
		Death:
		XDeath:
			TNT1 A 0 
			{
				A_SetScale(1.0);
				A_StartSound("Projectile/Explode", flags:CHANF_OVERLAP);
			}
			EXD3 ABCDE 2 Bright;
		stop;
	}
}
