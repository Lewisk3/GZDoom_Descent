class Descent_SpreadFire : DSCWeapon
{
	bool vert;
	
	Default
	{
		Weapon.SlotNumber 2;
		Tag "Spread";
		DSCWeapon.WeaponName "Spreadfire Cannon";
		DSCWeapon.DisplayGraphic "WeapSpreadFire";
		DSCWeapon.SetupCrosshairs "Crosshair/weapon1", "Crosshair/weapon0";
	}
	
	/*
	action void A_FireSpread()
	{
		double soffs = -6;
		for(int i = 0; i < 5; i++)
		{
			vector2 spread = (soffs, 0);
			spread = RotateVector(spread, invoker.fireAngle);
			
			let proj = DSCProjectile(A_ShootProjectile("DSCProj_Spreadfire", "SpreadFire/Fire", true, 0,0,0, (20,0,-10)));
			if(proj) proj.localAccel = (0,spread.x,spread.y + 1);
			soffs += 3;
		}
	}
	*/
	
	action void A_FireSpread()
	{
		double soffs = -3;
		for(int i = 0; i < 3; i++)
		{
			vector2 spread = (soffs, 0);
			if(invoker.vert) spread = RotateVector(spread, 90);
			
			let proj = DSCProjectile(A_ShootProjectile("DSCProj_Spreadfire", "SpreadFire/Fire", true, 0,0,0, (20,0,-10)));
			if(proj) proj.localAccel = (0,spread.x,spread.y + 1);
			soffs += 3;
		}
	}
	
	States
	{
		Ready:
			TNT1 A 1 A_WeaponReady();
		Loop;
		
		Fire:
			TNT1 A 7
			{
				A_FireSpread();
				invoker.vert = !invoker.vert;
			}
		Goto Ready;
		
		Spawn:
			SPRF ABCDEFGHIJKLMNO 5;
		loop;
	}
}

class DSCProj_Spreadfire : DSCProjectile
{
	Default
	{	
		Radius 5;
		Height 5;
		Speed 40;
		DamageFunction 10 * DSCDMG;
		Scale 0.12;
		DSCProjectile.EnergyUse 0.13;
		Decal "Scorch";
	}
	
	States
	{
		Spawn:
			SPRD A 1 Bright;
		loop;
	
		Crash:
		Death:
		XDeath:
			TNT1 A 0 A_SetScale(1.0);
			EXDB ABCDE 2 Bright A_StartSound("Projectile/Explode", flags:CHANF_OVERLAP);
		stop;
	}
}
