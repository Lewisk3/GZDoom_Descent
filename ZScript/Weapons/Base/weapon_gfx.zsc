class MissileTrail : Actor
{
	Default
	{
		+NOGRAVITY;
		-SOLID;
		+NOINTERACTION;
		RenderStyle "Add";
		Alpha 0.60;
		Scale 0.35;
	}
	
	States
	{
		Spawn:
			EXPP AB 8;
		FadeLoop:
			EXPP C 1 A_FadeOut(0.125);
		wait;
		Death:
			EXPP C 1 A_FadeOut(0.125);
		stop;
	}
}

class MissileExplosion : Actor
{
	Default
	{
		+NOINTERACTION;
		+ROLLSPRITE;
		Scale 0.75;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				DSCEffects.ParticlesFX(self, "#C45632", amt:30, 5, 8);
				A_StartSound("Missile/Explode", CHAN_BODY, CHANF_OVERLAP);
				A_SetRoll(frandom(-90,90));
			}
			EXD6 ABCDEFGHIJKL 2 Bright;
		stop;
	}
}

class MiniExplosion : MissileExplosion
{
	Default
	{
		Scale 0.5;
	}

	States
	{
		Spawn:
			EXD3 AAABCDE 2 Bright;
		stop;
	}
}

class PyroGZ_Explosion : MissileExplosion
{	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_StartSound("MegaMissile/Explode", CHAN_BODY, CHANF_OVERLAP);
			EXDR ABCDEFGHIJKLMNOPQR 2 Bright;
		stop;
	}
}

class MegaMissileExplosion : MissileExplosion
{
	Default
	{
		Scale 4.5;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				DSCEffects.ParticlesFX(self, "#C45632", amt:60, 5, 8, (100,100,100),(40,40));
				A_StartSound("MegaMissile/Explode", CHAN_BODY, CHANF_OVERLAP);
				A_SetRoll(frandom(-90,90));
			}
			EXDV ABCDEFGHIJKLM 2 Bright;
		stop;
	}
}


class EarthshakerExplosion : MissileExplosion
{
	Default
	{
		Scale 10.0;
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				S_StartSound("Earthshaker/Explode", CHAN_BODY, CHANF_OVERLAP);
				A_SetRoll(frandom(-90,90));
			}
			EXDZ ABCDEFGHIJKLM 2 Bright;
		stop;
	}
}
class EarthshakerChildExplosion : EarthshakerExplosion
{	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				S_StartSound("Missile/Explode", CHAN_BODY, CHANF_OVERLAP);
				DSCEffects.ParticlesFX(self, "#C45632", amt:60, 5, 8, (100,100,100),(40,40));
				A_SetRoll(frandom(-90,90));
			}
			EXDZ ABCDEFGHIJKLM 2 Bright;
		stop;
	}
}


class FlashExplosion : MissileExplosion
{
	Default
	{
		Scale 4.5;
	}
	
	void DoFlash()
	{
		// Check for nearby players
		let it = BlockThingsIterator.Create(self, 1024);
		while(it.Next())
		{
			let act = it.Thing;
			double dist = Distance3D(act);
			if(!act.bSHOOTABLE) continue;
			if(act.health <= 0) continue;
			if(!IsVisible(act, true)) continue;
			if(dist > 360) continue; // Range of blast
			
			// Flash player(s) screen.
			let plr = DescentPlayer(act);
			if(plr)
			{
				DSCEffects.ScreenFlash(plr, "White", plr.screen_fade+0.8);
				continue;
			}			
			
			act.GiveInventory("Effect_BlindMe", 1);
		}
	}
	
	States
	{
		Spawn:
			TNT1 A 0 NoDelay
			{
				DoFlash();
				DSCEffects.ParticlesFX(self, "#C45632", amt:60, 5, 8, (100,100,100),(40,40));
				A_StartSound("FlashMissile/Explode", CHAN_BODY, CHANF_OVERLAP);
				A_SetRoll(frandom(-90,90));
			}
			EXD6 ABCDEFGHIJKL 2 Bright;
		stop;
	}
}


class ImpactExplosion : MissileExplosion
{	
	Default
	{
		Scale 0.40;
	}
	
	States
	{
		Spawn:
			EXDI ABCDEFGH 2 Bright;
		stop;
	}
}

class EnemyExplosion : MissileExplosion
{	
	Default
	{
		Scale 0.65;
	}

	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_StartSound("Missile/Explode", CHAN_BODY, CHANF_OVERLAP);
			TNT1 A 0 A_Jump(255, "One", "Two", "Three");
		One:
			EXDD ABCDEFGHIJKJMNOPQ 2 Bright;
		stop;
		Two:
			EXDF ABCDEFGHIJKJMNOPQ 2 Bright;
		stop;
		Three:
			EXDJ ABCDEFGHIJK 2 Bright;
		stop;
	}
}


