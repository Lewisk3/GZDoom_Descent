class VulcanAmmo : Ammo
{
	mixin DSCAmmoGenerics;
	
	Default
	{
		Inventory.Amount 2500;
		Inventory.MaxAmount 20000;
		Inventory.PickupMessage "Vulcan Ammo!";
	}
	
	States
	{
		Spawn:
			VLCA ABCDEFGHIJKLMNO 5;
		loop;
	}
}

class Descent_VulcanCannon : DSCWeapon
{
	Default
	{
		Weapon.SlotNumber 3;
		Tag "Vulcan";
		
		Weapon.AmmoType1 "VulcanAmmo";
		Weapon.AmmoGive1 2500;
		
		DSCWeapon.WeaponName "Vulcan Cannon";
		DSCWeapon.DisplayGraphic "WeapVulcan";
		DSCWeapon.SetupCrosshairs "Crosshair/weapon1", "Crosshair/weapon0";
	}
	
	override string GetUIDescription()
	{
		return String.Format("\c[DarkGreen]%s\n\n\c[Red]%.3d", GetTag().MakeUpper(), Owner.countinv("VulcanAmmo"));
	}
	
	States
	{
		Ready:
			TNT1 A 1 A_WeaponReady();
		Loop;
		
		Fire:
			TNT1 A 2
			{
				A_ShootProjectile("VulcanBullet", "VulcanCannon/Fire", true, offs:(20, frandom(-5,5), frandom(-8,8)));
			}
		Goto Ready;
		
		Spawn:
			VULC ABCDEFGHIJKLMNO 5;
		loop;
	}
}


class VulcanBullet : DSCProjectile
{
	Default
	{	
		Radius 2;
		Height 2;
		Scale 0.65;
		Damage 4 * DSCDMG;
		+ROLLSPRITE;
		DSCProjectile.SetupAmmo "VulcanAmmo", 13; // 12.75 per-shot.
	}
		
	override void Tick()
	{
		super.Tick();
		bInvisible = GetAge() <= 5;
	}

	States
	{	
		Spawn:
			MODL A 0 NoDelay DoHitscan();
		SpawnLoop:
			TNT1 A 1;
		loop;
		
		Death:
		Crash:
		XDeath:
			LS1H AAABCDE 3 Bright;
		stop;
	}
}