class Descent_EarthshakerMissile : DSCSpecialWeapon
{
	bool fireLeft;

	Default
	{
		DSCSpecialWeapon.Tag "Shaker\n\c[DarkGreen]MISSILE";
		DSCSpecialWeapon.WeaponName "Earthshaker Missile";
		DSCSpecialWeapon.DisplayGraphic "shakerMissile";
		DSCSpecialWeapon.SetupCrosshairs "Crosshair/rocket1", "Crosshair/rocket0";
		DSCSpecialWeapon.SlotNumber 10;
		Inventory.MaxAmount 10 * DSCMAXAMMO;
	}
	
	override bool Fire()
	{
		if(cooldown || raiseY < 1.0) return false;
		if(amount <= 0) return false;
		
		if(ShootProjectile("DSCProj_ShakerMissile", offs:(10,0,-10)))
		{	
			SetState(FindState("DoRecoil"));
			cooldown = 70;
			Deplete();
		}
		
		return true;
	}
	
	States
	{
		Spawn:
			EMIS ABCDEFGHIJKLMNO 5;
		loop;
		DoRecoil:
			TNT1 A 0 
			{
				DSCEffects.ViewRecoil(Owner, -10.0, -10.0, 0);
				DSCEffects.RecoilPush(Owner, -10,0,0);
			}
			TNT1 A 5;
			TNT1 A 0 
			{
				DSCEffects.ViewRecoil(Owner, 5.0, 5.0, 0);
			}
		goto Spawn;
	}
}

class DSCProj_ShakerMissile : DSCProj_ConMissile
{
	bool blink;
	Default
	{
		Speed 20;
		DamageFunction DescentDMG(500);
		DSCProjectile.FireSound "Earthshaker/Fire";
		DSCProjectile.SetMissileCamera true;
		Tag "Earthshaker";
	}
	
	override void DoPayload() 
	{
		for(int i = 0; i < 6; i++)
		{
			vector3 views = (
				 frandom[EarthShake](-90,90) + 180,
				-pitch + frandom[EarthShake](-30,30), 
				 roll
			);
			DSCEffects.ShootProjectile(self, "DSCProj_ShakerTracker", false, views.x,views.y,views.z);
		}
	}
	
	States
	{
		Spawn:
			MODL A 1 
			{	
				SpawnTrail();	
				Spawn("MiniExplosion", pos);
			}
		loop;
		
		Crash:
		Death:
		XDeath:
			BOOM A 1 
			{
				DoMissileExplosion(0,0, 10.0, type:"EarthshakerExplosion");
				A_QuakeEx(6,6,6, 90, 0, 1024, "Earthshaker/Quake", QF_SCALEDOWN);
				A_StartSound("Earthshaker/Quake", CHAN_BODY, CHANF_OVERLAP|CHANF_LOOPING, attenuation:ATTN_NONE);
				DoPayload();
			}
			"####" AAAAAAAAAAAAAAAAAAAAAAAAAAAAAAAA 4
			{	
				// Blinking lighting effects.
				Name boomSprite = "BOOM";
				if(blink) boomSprite = "TNT1";
				Sprite = GetSpriteIndex(boomSprite);
				blink = random[EarthShake](0,1);
				if(GetAge() < 20) blink = false;
				
				DSCEffects.ViewRecoil(target, frandom[EarthShake](-2,2), frandom[EarthShake](-2,2), frandom[EarthShake](-2,2));
				A_ExplodeEx(30, 1024);
			}
			// Flash lighting effects.
			TNT1 A 0 A_StopSound(CHAN_BODY);
		stop;
	}
}

class DSCProj_ShakerTracker : DSCProj_SmartTracker
{
	Default
	{
		Scale 0.25;
		Speed 10;
		DamageFunction DescentDMG(100);
	}
	
	States
	{
		Spawn:
			MODL A 1 Bright
			{	
				Spawn("MiniExplosion", pos);
			}
		loop;
		
		Crash:
		Death:
		XDeath:
			BOOM A 1 Bright DoMissileExplosion(300, 128, 2.0, type:"EarthshakerChildExplosion");
		stop;
	}
}